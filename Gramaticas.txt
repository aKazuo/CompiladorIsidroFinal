Gramática

prog       -> Programa decl bloco Fimprog
decl       -> (declaravar)+
declaravar -> tipo ID  (VIR ID)* SC
tipo       -> 'numero' | 'texto'
bloco      -> (cmd)+
cmd        -> cmdleitura | cmdescrita | cmdattrib | 
	      cmdselecao | cmdtesta | cmdenq 
cmdleitura -> Leia AP ID FP SC
cmdescrita -> Escreva AP (ID | TEXTO | cmdmat) FP
cmdattrib  -> ID ATTR (expr | TEXTO) SC
cmdselecao -> Se AP ID OPREL (ID | NUMBER | TEXTO) FP
	      Entao ACH (cmd)+ FCH
	      (Senao ACH  (cmd)+ FCH)?
cmdmat     -> cmdexp | cmdraiz | cmdlog
cmdexp     -> Exponencial AP (ID | NUMBER) VIR NUMBER FP
cmdraiz    -> Raiz AP (ID | NUMBER) FP
cmdlog     -> Logaritmo AP (ID | NUMBER) FP
cmdtesta   -> Testa AP ID FP 
              ACH
              (Caso (NUMBER | TEXTO) DPT
              ACH (cmd)+ FCH
	      )+
              FCH
cmdenq     -> Enquanto AP ID OPREL (ID | NUMBER | TEXTO) FP 
              ACH (cmd)+ FCH
expr       -> expr (OPSOM | OPSUB) termo | termo
termo      -> termo (OPMUL | OPDIV) fator | fator
fator      -> NUMBER | ID | cmdmat | AP expr FP 

OBS:  É possível apenas usar os comandos de exp, raiz e log em nos comandos de seleção, escrever e atribuição
OBS2: 

Tokens

TEXTO  -> '"' (~'"')* '"'
OPREL  -> '<=' | '>=' | "<" | ">" | "!=" | "=="   (Operador de Relação)
VIR    -> ','                                     (Operador Virgula)
AP     -> '('                                     (Operador Abre Parenteses)
FP     -> ')'                                     (Operador Fecha Parenteses)
SC     -> '.'                                     (Operador de Pontuação)
OPSOM  -> '+'			                  (Operador Soma)
OPSUB  -> '-'			                  (Operador Subtração)
OPMUL  -> '*'			                  (Operador Multiplicação)
OPDIV  -> '/'			                  (Operador Divisão)
ATTR   -> ':='                                    (Operador de Atribuição)
ACH    -> '{'                                     (Operador Abre Chave)
FCH    -> '}'                                     (Operador Fecha Chave)
DPT    -> ':'                                     (Operador Dois Pontos)
ID     -> (a..z | A..Z)(a..z | A..Z | 0..9)*          
NUMBER -> (0..9)+('.'(0..9)+)?                    
Programa -> 'programa'                           
Fimprog  -> 'fimprog.'                           
Declare  -> 'declare'                             
Leia     -> 'leia'                                
Escreva  -> 'escreva'                             
Se       -> 'se'                                
Senao    -> 'senao'                               
Entao    -> 'entao'                               
Exponencial -> 'exp'                              
Raiz     -> 'raiz'                               
Logaritmo-> 'log'                                
Testa    -> 'testa'                              
Caso     -> 'caso'                               
Enquanto -> 'enquanto' 
